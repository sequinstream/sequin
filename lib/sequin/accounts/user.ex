defmodule Sequin.Accounts.User do
  @moduledoc false
  use Sequin.ConfigSchema

  import Ecto.Changeset
  import Ecto.Query, only: [from: 2]

  alias Sequin.Accounts.AccountUser
  alias Sequin.Accounts.User

  @derive {Jason.Encoder,
           only: [
             :id,
             :name,
             :email,
             :auth_provider,
             :auth_provider_id,
             :inserted_at,
             :updated_at
           ]}

  @type id :: String.t()

  typed_schema "users" do
    field :name, :string
    field :email, :string
    field :password, :string, virtual: true, redact: true
    field :hashed_password, :string, redact: true
    field :current_password, :string, virtual: true, redact: true
    field :confirmed_at, :utc_datetime
    field :auth_provider, Ecto.Enum, values: [:identity, :github]
    field :auth_provider_id, :string
    field :extra, :map

    has_many :accounts_users, AccountUser
    has_many :accounts, through: [:accounts_users, :account]
    field :impersonating_user, :map, virtual: true

    field :last_login_at, :utc_datetime

    timestamps(type: :utc_datetime)
  end

  def where_account_id(query \\ base_query(), account_id) do
    from([user: u] in query,
      join: ua in AccountUser,
      on: ua.user_id == u.id,
      where: ua.account_id == ^account_id
    )
  end

  def current_account(%User{} = user) do
    user = Sequin.Repo.preload(user, [:accounts_users, :accounts])

    user.accounts_users
    |> Enum.find(& &1.current)
    |> case do
      nil ->
        Enum.min_by(user.accounts, & &1.inserted_at, DateTime)

      account_user ->
        account_user.account
    end
  end

  defp base_query(query \\ __MODULE__) do
    from(u in query, as: :user)
  end

  @doc """
  A user changeset for registration.

  It is important to validate the length of both email and password.
  Otherwise databases may truncate the email without warnings, which
  could lead to unpredictable or insecure behaviour. Long passwords may
  also be very expensive to hash for certain algorithms.

  ## Options

    * `:hash_password` - Hashes the password so it can be stored securely
      in the database and ensures the password field is cleared to prevent
      leaks in the logs. If password hashing is not needed and clearing the
      password field is not desired (like when using this changeset for
      validations on a LiveView form), this option can be set to `false`.
      Defaults to `true`.

    * `:validate_email` - Validates the uniqueness of the email, in case
      you don't want to validate the uniqueness of the email (like when
      using this changeset for validations on a LiveView form before
      submitting the form), this option can be set to `false`.
      Defaults to `true`.
  """
  def registration_changeset(user, attrs, opts \\ []) do
    user
    |> cast(attrs, [:email, :password, :name])
    |> validate_email(opts)
    |> validate_password(opts)
    |> put_change(:auth_provider, :identity)
    |> check_constraint(:auth_provider, name: :auth_provider_constraint, message: "Invalid authentication provider setup")
  end

  def provider_registration_changeset(user, attrs) do
    user
    |> cast(attrs, [:email, :name, :auth_provider, :auth_provider_id, :extra])
    |> validate_required([:email, :auth_provider, :auth_provider_id])
    |> validate_email([])
    |> unique_constraint([:auth_provider, :auth_provider_id])
    |> check_constraint(:auth_provider, name: :auth_provider_constraint, message: "Invalid authentication provider setup")
  end

  # Add this function to handle GitHub profile updates
  def github_update_changeset(user, attrs) do
    cast(user, attrs, [:name, :email, :extra])
  end

  def update_changeset(user, attrs) do
    cast(user, attrs, [:name, :last_login_at])
  end

  defp validate_email(changeset, opts) do
    changeset
    |> validate_required([:email])
    |> validate_format(:email, ~r/^[^\s]+@[^\s]+$/, message: "must have the @ sign and no spaces")
    |> validate_length(:email, max: 160)
    |> maybe_validate_unique_email(opts)
  end

  defp validate_password(changeset, opts) do
    changeset
    |> validate_required([:password])
    |> validate_length(:password, min: password_min_length(), max: 72)
    # Examples of additional password validation:
    # |> validate_format(:password, ~r/[a-z]/, message: "at least one lower case character")
    # |> validate_format(:password, ~r/[A-Z]/, message: "at least one upper case character")
    # |> validate_format(:password, ~r/[!?@#$%^&*_0-9]/, message: "at least one digit or punctuation character")
    |> maybe_hash_password(opts)
  end

  defp maybe_hash_password(changeset, opts) do
    hash_password? = Keyword.get(opts, :hash_password, true)
    password = get_change(changeset, :password)

    if hash_password? && password && changeset.valid? do
      changeset
      # Hashing could be done with `Ecto.Changeset.prepare_changes/2`, but that
      # would keep the database transaction open longer and hurt performance.
      |> put_change(:hashed_password, Argon2.hash_pwd_salt(password))
      |> delete_change(:password)
    else
      changeset
    end
  end

  defp maybe_validate_unique_email(changeset, opts) do
    if Keyword.get(opts, :validate_email, true) do
      changeset
      |> unsafe_validate_unique(:email, Sequin.Repo)
      |> unique_constraint(:email)
    else
      changeset
    end
  end

  @doc """
  A user changeset for changing the email.

  It requires the email to change otherwise an error is added.
  """
  def email_changeset(user, attrs, opts \\ []) do
    user
    |> cast(attrs, [:email])
    |> validate_email(opts)
    |> case do
      %{changes: %{email: _}} = changeset -> changeset
      %{} = changeset -> add_error(changeset, :email, "did not change")
    end
  end

  @doc """
  A user changeset for changing the password.

  ## Options

    * `:hash_password` - Hashes the password so it can be stored securely
      in the database and ensures the password field is cleared to prevent
      leaks in the logs. If password hashing is not needed and clearing the
      password field is not desired (like when using this changeset for
      validations on a LiveView form), this option can be set to `false`.
      Defaults to `true`.
  """
  def password_changeset(user, attrs, opts \\ []) do
    user
    |> cast(attrs, [:password])
    |> validate_confirmation(:password, message: "does not match password")
    |> validate_password(opts)
  end

  @doc """
  Confirms the account by setting `confirmed_at`.
  """
  def confirm_changeset(user) do
    now = DateTime.truncate(DateTime.utc_now(), :second)
    change(user, confirmed_at: now)
  end

  @doc """
  Verifies the password.

  If there is no user or the user doesn't have a password, we call
  `Argon2.no_user_verify/0` to avoid timing attacks.
  """
  def valid_password?(%User{hashed_password: hashed_password}, password)
      when is_binary(hashed_password) and byte_size(password) > 0 do
    Argon2.verify_pass(password, hashed_password)
  end

  def valid_password?(_, _) do
    Argon2.no_user_verify()
    false
  end

  @doc """
  Validates the current password otherwise adds an error to the changeset.
  """
  def validate_current_password(changeset, password) do
    changeset = cast(changeset, %{current_password: password}, [:current_password])

    if valid_password?(changeset.data, password) do
      changeset
    else
      add_error(changeset, :current_password, "is not valid")
    end
  end

  defp password_min_length do
    case Application.get_env(:sequin, :env) do
      :dev -> 8
      _ -> 12
    end
  end
end
